plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.pakybytes'
version '1.0.1'

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    deployerJars
}

/** Repos
 */
repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
}

dependencies {
    // Encryption
    implementation group: "org.abstractj.kalium", name: "kalium", version: "0.8.0"

    // Tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'

    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.23.4'
}


/** Signing
 */
signing {
    sign configurations.archives
}


/** Publishing
 */
publishing {
    publications {
        mavenJava(MavenPublication) {

            customizePom(pom)

            groupId project.group
            artifactId project.name
            version project.version

            from components.java

            tasks.withType(Jar) {
                from(project.projectDir) {
                    include 'LICENSE.md'
                    into 'META-INF'
                }
            }

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }


            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//            url "https://oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Security utils for encryption and decryption'
            name project.name
            url 'https://github.com/HugoTigre/pb-sec'
            organization {
                name project.group
                url 'https://github.com/HugoTigre'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/HugoTigre/pb-sec/issues'
            }
            licenses {
                license {
                    name 'MIT'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/HugoTigre/pb-sec'
                connection 'scm:git:git://github.com/HugoTigre/pb-sec.git'
                developerConnection 'scm:git:ssh://git@github.com/HugoTigre/pb-sec.git'
            }
            developers {
                developer {
                    id "HugoTigre"
                    name "Hugo Tigre"
                    email "hugotigre@gmail.com"
                    organization 'pakybytes'
                }
            }
        }
    }
}

// generate the pom of the project (without publishing it) in case we need it
model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}